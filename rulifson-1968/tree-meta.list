    1 /* --0-- 
    2  * Tree Meta - PROGRAM SOURCE - 29 DEC 1967 
    3  */ 
    4  
    5 .META PROGRAM   /* TREE 1.3 */ 
    6  
    7 PROGRAM = (".META" .ID ?1? (".LIST" :LIST[0] / .EMPTY :MT[0]) SIZE :BEGIN[3] 
TREE 1.3
    8           / ".CONTINUE" :MT[0]) 
    9           <"TREE 1.3"> /* LISTUNIT */ :SETUP[1] * $( RULE * ) 
   10           ".END" ?2E :ENDN[0] * /* <"DONE">LISTUNIT */ ; 
   11 SIZE = '( SIZ $(', SIZ :DO[2]) ') ?50E / .EMPTY :MT[0] ; 
   12 SIZ = .CHR '= ?54E .NUM ?55E :SIZS[2] ; 
   13  
   14 RULE = .ID ('= EXP ?3E ('% /*'&*/ :KPOPK[1] / .EMPTY) :OUTPT[2] / 
   15        '/ "=>" ?3E GEN1 :SIMP[2] / 
   16        OUTRUL :OUTPT[2]) ?5E '; ?6E ; 
   17  
   18 EXP = "<-" SUBACK ?7E ('/ EXP ?8E :BALTER[2] / .EMPTY :BALTER[1]) / 
   19       SUBEXP ('/ EXP ?9E :ALTER[2] / .EMPTY) ; 
   20  
   21 SUBACK  =  NTEST (SUBACK :DO[2] / .EMPTY) / 
   22            STEST (SUBACK :CONCAT[2] / .EMPTY) ; 
   23  
   24 SUBEXP = (NTEST / STEST) (NOBACK :CONCAT[2] / .EMPTY) ; 
   25  
   26 NOBACK = (NTEST / STEST ('? .NUM ?10E :LOAD[1] (.ID / '? :ZRO[0]) ?11E :ERCOD[3] / 
   27                          .EMPTY :ER[1]) ) 
   28          (NOBACK :DO[2] / .EMPTY) ; 
   29  
   30 NTEST = ': .ID ?12E :NDLB[1] / 
   31         '[ (.NUM '] ?14E :MKNODE[1] / 
   32             GENP '] ?52E (': / .EMPTY :OUTCH[0] :DO[2]) ) / 
   33         '< GENP '> ?53E (': / .EMPTY :OUTCH[0] :DO[2]) :TTY[1] / 
   34         (".CHR" :GCHR / '* :GO) [0] / 
   35         "=>" STEST ?15E :SCAN[1] / 
   36         COMM ; 
   37  
   38 GENP = GENP1 / .EMPTY :MT[0] ; 
   39   
   40 GENP1 = GENP2 (GENP1 :DO[2] / .EMPTY) ; 
   41  
   42 GENP2 = '* ('S .NUM ?51E :PAROUT[1] / .EMPTY :ZRO[0] :PAROUT[1]) 
   43            ('L :OL / 'C :OC / 'N :ON / .EMPTY :OS) [0] :NOPT[2] / 
   44         GENU ; 
   45  
   46 COMM = ".EMPTY" :SET[0] / 
   47        '! .SR ?18E :IMED[1] ; 
   48  
   49 STEST = '. .ID ?19E :PRIM[1] / 
   50         .ID :CALL[1] / 
   51         .SR :STST[1] / 
   52         '( EXP ?20E ') ?21E / 
   53         '' .CHR :CTST[1] / 
   54         (.NUM '$ ?23E / '$ :ZRO[0]) (.NUM / .EMPTY :MT[0]) STEST ?20E :ARB[3] / 
   55         '- (.SR :NSR[1] / '' .CHR :NCHR[1]) ?26E :NTST[1] ; 
   56 /* --1-- 
   57  * Tree Meta - PROGRAM SOURCE - 29 DEC 1967 
   58  */ 
   59  
   60 OUTRUL = '[ OUTR ?27E (OUTRUL :ALTER[2] / .EMPTY) :OSET[1] ; 
   61 OUTR = OUTEST "=>" ?29E OUTEXP ?30E :CONCAT[2] ; 
   62 OUTEST = (('] :MT / "-]" :ONE / "-,-]" :TWO / "-,-,-]" :THRE) [0] / 
   63           ITEMS ']) :CNTCK[1] ; 
   64  
   65 ITEMS = ITEM (', ITEMS ?32E :ITMSTR[2] / .EMPTY :LITEM[1]) ; 
   66 ITEM = '- :MT[0] / 
   67        .ID '[ ?33E OUTEST ?34E :RITEM[2] / 
   68        NSIMP1 :NITEM[1] / 
   69        '. .ID ?35E :FITEM[1] / 
   70        .SR :TTST[1] / 
   71        '' .CHR :CHTST[1] / 
   72        '# .NUM  ?37E :GNITEM[1] ; 
   73  
   74 OUTEXP = SUBOUT ('/ OUTEXP :ALTER[2] / .EMPTY) ; 
   75 SUBOUT = OUTT (REST :CONCAT[2] / .EMPTY) / REST ; 
   76  
   77 REST = OUTT (REST :OER[2] / .EMPTY) / GEN (REST :DO[2] / .EMPTY) ; 
   78 OUTT = .ID '[ ?39E ARGLST '] ?40E :OUTCLL[2] / '( OUTEXP ') ?41E / 
   79         NSIMP1 (': ('S :OS / 'L :OL / 'N :ON / 'C :OC) [0] :NOPT[2] / 
   80         .EMPTY :DOIT[1]) ; 
   81  
   82 ARGLST = ARGMNT :ARG[1] (', ARGLST :DO[2] / .EMPTY) / .EMPTY :MT[0] ; 
   83 ARGMNT = NSIMP :ARGLD[1] / '# .NUM :GENARG[1] ; 
   84  
   85 NSIMP1 = <- '^ NSIMP :UP[2] / NSIMP :LKT[1] ; 
   86  
   87 NSIMP = '* .NUM ( <- ': NSIMP :CHASE[2] / .EMPTY :LCHASE[1]) ; 
   88  
   89 GEN1 = (OUT/COMM) (GEN1 :DO[2] / .EMPTY) ; 
   90  
   91 GEN =  COMM / GENU  / '< :TTY[0] / '> :FIL[0] ; 
   92  
   93 GENU = OUT / 
   94        '. .ID ?42E '[ ?43E ((.ID / .NUM) :LOAD[1] :CALL[2] / 
   95                             .EMPTY :CALL[1]) '] / 
   96        '# .NUM :GNLBL[1] (': :DEF[1] / .EMPTY) ; 
   97  
   98 OUT = ('\ :OUTCR / ', :OUTAB) [0] / 
   99        .SR :OUTSR[1] / 
  100        '' .CHR :OUTCH[1] / 
  101        "+W" :UPWRK[0] :OUTWRK[1] / 
  102        "-W" :DNWRK[0] / 
  103        ".W" :MT[0] :OUTWRK[1] / 
  104        "^W" :MAXWRK[0] ; 
  105  
  106 E  =  .EMPTY RESET => '; $(RULE *) ".END" ?99E FINISH ; 
  107 RESET = .EMPTY ; /* [ ,";BRM RESET" ] ; */ 
  108 FINISH = .EMPTY ; /* [ ,";BRM FINISH" ] ; */ 
  109  
  110 /* --2-- 
  111  * Tree Meta - PROGRAM SOURCE - 29 DEC 1967 
  112  */ 
  113  
  114 SETUP [-] => ,"NOLIST NUL,EXT;GEN OPD 101B5,1,1;BF OPD 102B5,1,1" \ 
  115               "BT OPD 103B5,1,1;PSHN OPD 104B5,1,1;PSHK OPD 105B5,1,1" \ 
  116               "MKND OPD 106B5,1,1;NDLBL OPD 107B5,1,1;GET OPD 110B5,1,1" \ 
  117               "BPTR OPD 111B5,1,1;BNPTR OPD 112B5,1,1;RI1 OPD 113B5,1,1" \ 
  118               "RI2 OPD 114B5,2;FLGT OPD 115B5,1,1;BE OPD 116B5,1,1" \ 
  119               "LAB OPD 117B5,1,1;CE OPD 120B5,1,1;LDKA OPD 121B5,1,1" \ 
  120     "$KSTKSZ EQU 100;$MSTKSZ EQU 130;$NSTKSZ EQU 1300;$SSTKSZ EQU 1400" \ *1 ; 
  121  
  122 BEGIN [-,-,-] => "$START BRM INITL; CLA; STA WRK; STA XWRK" \ 
  123                  *3 *2 , "BRM RLINE; BRM " *1 "; BRM FINISH" \ ; 
  124  
  125 LIST / => " CLA; STA LISTFG;" ; 
  126  
  127 OUTPT [-,-] => *1:S , "ZRO; LDA *-1; BRM CLLO" \ *2 , "BRU RTNO" \ ; 
  128  
  129 SIMP [-,-] => *1 , "ZRO" \ *2 , "BRR " *1 \ ; 
  130  
  131 BALTER [-] => , "BRM SAV" \ *1 , "BRM RSTR" \ 
  132      [-,-] => , "BRM SAV" \ *2 , "BRM RSTR; BT " #1 \ *2 #1 D[] ; 
  133  
  134 D / => , "EQU *" \ ; 
  135  
  136 ALTER [-,SET[]] => *1 *2 
  137 [CONCAT[-,-],-] => PMT[*1:*1,#1] *1:*2 , "BRU " #2 \ #1 D[] *2 #2 D[] \ 
  138           [-,-] => *1 , "BT " #1 \ *2 #1 D[] ; 
  139  
  140 PMT [PRIM[-],#1] => , "BRM " *1:*1:S "; BF " #1 "; MRG " *1:*1:S "FLG; PSHK =0" \ 
  141            [-,-] => *1 /*2*/ , "BF " #1 \ ; 
  142  
  143 ER [ALTER[-,SET[]]] => *1 
  144                 [-] => *1 , "BE =-1" \ ; 
  145  
  146 DO [-,-] => *1 *2 ; 
  147  
  148 CONCAT [-,-] => *1 , "BF " #1 \ *2 #1 D[] ; 
  149  
  150 LOAD [.NUM] => , "LDA =" *1:S \ 
  151       [.ID] => , "LDA " *1:S \ ; 
  152  
  153 CALL [-] => , "BRM " *1 \ 
  154    [-,-] => *2 , "BRM " *1 \ ; 
  155  
  156 MT / => .EMPTY ; 
  157  
  158 CLA / => "CLA" ; 
  159  
  160 ZRO / => "0" ; 
  161  
  162 /* --3-- 
  163  * Tree Meta - PROGRAM SOURCE - 29 DEC 1967 
  164  */ 
  165  
  166 ERCOD [-,-,-] => *1 *2 , "BE " *3 \ ; 
  167  
  168 NDLB [-] => , "NDLBL =" *1 \ ; 
  169  
  170 MKNODE [-] => , "MKND =" *1 \ ; 
  171  
  172 ARB [ZRO[],MT[],-] => #1 D[] *3 , "BT " #1 "; MIN MFLAG" \ 
  173      [.NUM,MT[],-] => ARB1[*1] #1 D[] *3 
  174                       , "SKR* MSP; BT " #1 "; SKN* MSP; BRU *+3; BT " #1 "; MIN MFLAG" \ ARB3[] 
  175         [-,.NUM,-] => ARB1[*2] #1 D[] *3 
  176                       , "SKR* MSP; BT " #1 "; SKN* MSP" \ ARB2[*1,*2] ; 
  177  
  178 ARB1 [-] => , "BRM SAV; LDA =" *1:S "+1; MIN MSP; STA* MSP" \ ; 
  179  
  180 ARB2 [-,.NUM] => , "BRM *+4; CLA; STA MFLAG; BRU *+4; LDA* MSP; SKG =" *2 "-" *1 
  181                  "; MIN MFLAG" \ ARB3[] 
  182           [-] => , "BRU *+3; CLA; STA MFLAG" \ ARB3[] ; 
  183  
  184 ARB3 / => , "LDA =-1; ADM MSP; BRM RSTR" \ ; 
  185  
  186 GCHR / => , "BRM WPREP; BRM INC; LDA* IWP; MRG CHRFLG; MIN NCCP; PSHK =0" \ ; 
  187  
  188 GO / => "BBM OUTREE; BT *+3; LDA =2; BRM CERR" \ ; 
  189  
  190 SET / => , "LDA =1; STA MFLAG" \ ; 
  191  
  192 TTY [-] => TTY[] *1 FIL[] 
  193      [] => , "LDA =1; STA FNUMO" \ XCHCH[] ; 
  194  
  195 FIL [] => , "LDA XFNUMO; STA FNUMO" \ ; 
  196  
  197 XCHCH / => , "LDA TCHNO; XMA CHNO; STA TCHNO" \ ; 
  198 STRING [-] => " DATA " *1 :L "; ASC " '' *1 '' \ ; 
  199  
  200 OSET [-] => , "BRM BEGN" \ *1 ; 
  201  
  202 CNTCK [-] => *1 , "CLB; SKE NCNT; STB MFLAG" \ ; 
  203  
  204 ONE / => , "LDA =1" \ ; 
  205 TWO / => , "LDA =2" \ ; 
  206 THRE / => , "LDA =3" \ ; 
  207  
  208 ITMSTR [-,-] => *1 , "MIN CNT; EAX - 1,2" \ *2 ; 
  209  
  210 LITEM [-] => *1 , "MIN CNT; LDA CNT" \ ; 
  211 RITEM [-,-] => , "RI1 =" *1 "; BRU " #1 \ *2 , "RI2" \ #1 D[] ; 
  212  
  213 OER [-,-] => *1 , "CE =1" \ *2 ; 
  214  
  215 /* --4-- 
  216  * Tree Meta - PROGRAM SOURCE - 29 DEC 1967 
  217  */ 
  218  
  219 OUTCLL [-,-] => , "LDA NSP; STA SNSP; NDLBL =" *1 "; CLA; STA CNT" \ 
  220                 , "LDA KT; STA ME" \ *2 
  221                 , "MKND CNT; PSHN SNSP; LDX KT; BRM* 0,2; BRM POPK" \ 
  222                 , "LDA* NSP; STA NSP" \ ; 
  223  
  224 ARGLD [-] => , "LDA ME" \ *1 ; 
  225  
  226 ARG [-] => *1 , "PSHK  =0; MIN CNT" \ ; 
  227  
  228 CHASE [-,-] => , "GET =" *1 "; BPTR *+3; LDA =3; BRM CERR" \ *2 ; 
  229  
  230 LCHASE [-] => , "GET =" *1 \ ; 
  231  
  232 DOIT [-]  => *1 ,"EBPTR " #1 "; CAX; PSHK =0; BRM* 0,2; BRM POPK; BRU *+2" \ 
  233              #1 D[] , "BRM OUTS" \ ; 
  234  
  235 NOPT [-,-] => *1 ,"BNPTR *+3; LDA =4; BRM " #1 \ ; 
  236  
  237 SCAN [-] => #1 D[] *1 ,"BT *+3; MIN NCCP; BRU " #1 \ ; 
  238  
  239 PRIM [-] => , "BRM " *1 "; BF *+3; MRG " *1 "FLG; PSHK =0" \ ; 
  240  
  241 STST [-] => , "BRM TST;" STRING[*1] ; 
  242 CTST [-] => , "LDA =" *1:N "; BRM TCH" \ ; 
  243  
  244 OS / => " BRM OUTS" \ ; 
  245 ON / => " ETR =77777B; BRM OUTN" \ ; 
  246 OL / => " CAX; LDA 0,2; BRM DUTN" \ ; 
  247 OC / => " ETR =377B,; CIO FNUMO; MIN CHNO" \ ; 
  248  
  249 GNLBL [-] => , "GEN GNLB" *1 \ ; 
  250 DEF [-] => *1 , "BRM LIT; DATA 6; ASC " '' " EQU *" '' \ ; 
  251  
  252 OUTCR / => , "BRM CRLF" \ ; 
  253 OUTAB / => , "BRM TAB" \ ; 
  254 OUTSR [-] => , "BRM LIT; " STRING[*1] ; 
  255 OUTCH [-] => , "LDA =" *1:N "; CIO FNUMO; MIN CHNO" \ ; 
  256  
  257 ENDN / => "SSTOP DATA SS+SSTKSZ-5; $SS BSS SSTKSZ" \ 
  258    "MSP DATA MSTK; SMSPT DATA MSTK+MSTKSZ-5;$MSTK BSS MSTKSZ" \ 
  259    "NSP DATA NSTK; SNSPT DATA NSTK+NSTKSZ-5;$NSTK BSS NSTKSZ" \ 
  260    "KSP DATA KSTK; SKSPT DATA KSTK+KSTKSZ-5;$KSTK BSS KSTKSZ" \ 
  261    "WRK  BSS 1;XWRK BSS 1; END" \ ; 
  262  
  263 SAVG [-] => , "BRM SAVGN" \ *1 , "BRMRSTGN" \ ; 
  264  
  265 /* --5-- 
  266  * Tree Meta - PROGRAM SOURCE - 29 DEC 1967 
  267  */ 
  268  
  269 IMED [-] => , *1 \ ; 
  270  
  271 NITEM [-] => , "STX INDX; LDA KT" \ 
  272              *1 "CLB; LDX INDX; SKE 0,2; STB MFLAG" \ ; 
  273  
  274 FITEM [-] => , "FLGT " *1:S "FLG" \ ; 
  275  
  276 TTST [-] => , "BRM SSTEST;" STRING[*1] ; 
  277  
  278 CHTST [-] => , "CLB; LDA =" *1:N "; MRG CHRFLG; SKE 0,2; STB MFLAG" \ ; 
  279  
  280 GNITEM [-] => , "FLGT GENFLG; ETR= 77777B; STA GNLB" *1:S \ ; 
  281  
  282 GENARG [-] => , "LAB GNLB" *1:S "; MRG GENFLG" \ ; 
  283  
  284 NTST [-] => , "LDA NCCP; STA SNCCP" \ *1 
  285             , "LDA =1; SKR MFLAG; BRU *+2; STA MFLAG; LDA SNCCP; STA NCCP" \ ; 
  286  
  287 NCHR [-] => , "LDA =" *1:N "; BRM TCH" \ ; 
  288  
  289 NSR [-] => , "BRM TST; " STRING[*1] ; 
  290  
  291 UP ["1",-] => , "LDA* KSP" \ *2 
  292      [-,-] => , "LDX KSP; LDA 1-" *1:S ",2" \ *2 ; 
  293  
  294 LKT [-] => , "LDA KT" \ *1 ; 
  295  
  296 UPWRK / => , "MIN WRK; LDA WRK; SKG XWRK; LDA XWRK; STA XWRK" \ ; 
  297 DNWRK / => , "LDA =-1; ADM WRK" \ ; 
  298 OUTWRK [-] => *1 , "LDA WRK; BRM OUTN" \ ; 
  299 MAXWRK / => , "LDA XWRK; BRM OUTN" \ ; 
  300  
  301 SIZS [.CHR,-] => *1:C "STKSZ EQU " *2:S \ ; 
  302  
  303 KPOPK [-] => , "MIN MSP; LDA KT; STA* MSP; MIN MSP; LDA KSP; STA* MSP" *1 \ 
  304              , "LDX MSP; LDA 0,2; STA KSP; LDA -1,2; STA KT; LDA =-2; AIX MSP" \ ; 
  305  
  306 PAROUT [ZRO[]] => , "LDA KT" \ 
  307          ["0"] => , "LDA KT" \ 
  308            [-] => , "LDKA =" *1 \ ; 
  309  
  310 .END 
DONE
 KSPMAX=1000 MSPMAX=-2048 NSPMAX=2048 SSPMAX=8254
 ELAPSED TIME 0:00     CPU TIME 0:00
